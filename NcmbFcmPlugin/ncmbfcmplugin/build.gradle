apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 14
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
repositories {
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.google.android.gms:play-services-base:17.0.0'
    compileOnly fileTree(dir: 'src/libs', include: ['*.aar', '*.jar'], exclude: [])
}

android.libraryVariants.all { variant ->
    variant.outputs.each { output ->
        output.packageLibrary.exclude('libs/classes.jar')
    }
}

task clearAar(type: Delete) {
    delete 'build/outputs/aar' + AAR_NAME
    delete 'release/' + AAR_NAME
}

task makeAar(type: Copy) {
    from('build/outputs/aar/')
    into('release/')
    include('ncmbfcmplugin-release.aar')
    rename('ncmbfcmplugin-release.aar', AAR_NAME)
}
makeAar.dependsOn(clearAar, 'assembleRelease')