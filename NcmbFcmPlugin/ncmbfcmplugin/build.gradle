apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
repositories {
    maven {
        url 'https://maven.google.com'
        // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: '../libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.google.firebase:firebase-messaging:17.1.0'
    implementation 'com.google.android.gms:play-services-base:15.0.1'
}

android.libraryVariants.all { variant ->
    variant.outputs.each { output ->
        output.packageLibrary.exclude('libs/classes.jar')
    }
}

task clearAar(type: Delete) {
    delete 'build/outputs/aar' + AAR_NAME
    delete 'release/' + AAR_NAME
}

task makeAar(type: Copy) {
    from('build/outputs/aar/')
    into('release/')
    include('ncmbfcmplugin-release.aar')
    rename('ncmbfcmplugin-release.aar', AAR_NAME)
}
makeAar.dependsOn(clearAar, 'assembleRelease')